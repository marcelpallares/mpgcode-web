---
title: 'Resizing MacOS window to 16:9 (AppleScript)'
seoTitle: 'Resizing MacOS window to 16x9 (AppleScript)'
excerpt: 'Using AppleScript to resize any MacOS app windows to 16:9 aspect ratio.'
coverImage: 'screen-recording.gif'
date: '2022-02-14T10:00:00.000Z'
isDraft: true
---

<img src="screen-recording.gif" alt="Resizing MacOS window to 16x9" width="500" />

Resizing a MacOS app window to 16:9 using AppleScript from the command line.

During the last week, I needed to resize a MacOS app window to a 16:9 aspect ratio because I wanted to screen record on the same ratio of the video canvas of the edit I was working on.

I couldn't find much useful information about this topic online, but one nice [post from Ethan Banks](https://ethancbanks.com/using-applescript-to-size-a-window-to-16x9-on-macos/) on how to use AppleScript for the manner gave me what I needed to get started.

I had some extra needs like selecting the application I wanted to resize from a list of apps and making the window full size with a maximum width constraint, so I have decided to write this blog sharing my own implementation. Cheers to [@ecbanks](https://twitter.com/ecbanks) for the initial version ðŸ˜„

## How to size a MacOS application to 16:9

The requirements for this script are:
* Showing a **list of available windows** to manually select which one to resize.
* Resizing the selected ones to **16:9 aspect ratio using the maximum width available**.
* Setting up a **max-width** constraint to avoid too large windows.

This is the code needed to do all of this:

```js
# Import needed libraries
use framework "Foundation"
use framework "AppKit"
use scripting additions

# Display selectable list of open applications and save the selection
tell application "System Events"
	set listOfProcesses to (name of every process where background only is false)
	tell me to set selectedApps to choose from list listOfProcesses with multiple selections allowed
end tell

# Get the available width and height of the primary screen (minus statusbar and dock)
set primaryDisplay to current application's NSScreen's screens()'s firstObject()'s visibleFrame()
set screenWidth to item 1 of item 2 of primaryDisplay as integer
set screenHeight to item 2 of item 2 of primaryDisplay as integer

# Ratio for our app window
set ratio to 16 / 9

# The maxWidth we want our window to be
set maxWidth to 2048 as integer

if screenWidth > maxWidth then
	set virtualScreenWidth to maxWidth as integer
end if

# Calculate new app height
set appHeight to virtualScreenWidth / ratio as integer

# If the calculated height is bigger than screen height, set app height as screen height
# and calculate the width accordingly
if appHeight > screenHeight then
	set appHeight to screenHeight as integer
	set appWidth to appHeight * ratio
else
	set appWidth to virtualScreenWidth as integer
end if

# Calculate app screen position (centered)
set xPos to (screenWidth - appWidth) / 2 as integer
set yPos to 0 as integer


# Loop through selected apps
repeat with selectedApp in selectedApps

	# Set app position and size to calculated values
	tell application "System Events" to tell process selectedApp
		tell window 1
			set position to {xPos, yPos}
			set size to {appWidth, appHeight}
		end tell
		get size of window 1
	end tell

end repeat
```

You can also find the code on my Github repository in the following link: [Github repo - Resize MacOS window to 16:9](https://github.com/marcelpallares/resize-window-macos)

